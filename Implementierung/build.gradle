plugins {
    id 'application'
    id 'java'
    id 'idea'
    id "com.github.harbby.gradle.serviceloader" version "1.1.5"
}

group 'de.swiss'
version '1.0-SNAPSHOT'

sourceCompatibility = 11

sourceSets {
    main.java.srcDir "src/main/java"
    test.java.srcDir "src/test/java"
}

repositories {
    mavenCentral()
    flatDir dirs: "${rootDir}/libs"
}

configurations {
    umlDoclet
}

mainClassName = 'de.sswis.controller.Sswis'

jar {
    manifest {
        attributes 'Main-Class': 'de.sswis.controller.Sswis'
    }
}

serviceLoader {
    serviceInterface 'de.sswis.model.strategies.BaseStrategy'
    serviceInterface 'de.sswis.model.conditions.Condition'
    serviceInterface 'de.sswis.model.algorithms.adaptation.AdaptationAlgorithm'
    serviceInterface 'de.sswis.model.algorithms.ranking.RankingAlgorithm'
    serviceInterface 'de.sswis.model.algorithms.pairing.PairingAlgorithm'
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    umlDoclet "nl.talsmasoftware:umldoclet:2.0.0-rc.4"
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'org.jgrapht:jgrapht-core:1.3.0'
    compile 'com.intellij:forms_rt:7.0.3'
}

javadoc {
    source = sourceSets.main.allJava
    options.docletpath = configurations.umlDoclet.files as List
    options.doclet = "nl.talsmasoftware.umldoclet.UMLDoclet"
}

task texdoc(group: "documentation", description: "Generates Javadoc as TeX document", type: Exec, dependsOn: javadoc) {
    executable 'javadoc'
    args '-docletpath', 'TeXDoclet.jar',
            '-doclet', 'org.stfm.texdoclet.TeXDoclet',
            '-noindex',
            '-tree',
            '-hyperref',
            '-nosummaries',
            '-nofieldsummary',
            '-noconstructorsummary',
            '-shortinherited',
            '-include',
            '-sectionlevel', 'chapter',
            '-output', '../Entwurf/javadoc.tex',
            '-title', 'Entwurfsheft',
            '-date', '23.12.2018',
            '-author', 'Michel Bodé, Max Braun, Sophie Bräuniger, Simon Hügel, Zezhong Tong',
            '-sourcepath', 'src/main/java',
            '-subpackages', 'de.sswis'
}
